#@TYPE: Machine
#@NAME: swi-mdm9x15
#@DESCRIPTION: Machine configuration for swi-mdm9x15 systems

# Ship all kernel modules by default. This affects only images based on
# packagegroup-base (which we are not using), which does not include the
# core-image-minimal or core-image-full-cmdline images.
# If you want these to appear in the system regardless of the base
# packages, use IMAGE_INSTALL instead.
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules"

# lk bootloader is used by mdm9x15
EXTRA_IMAGEDEPENDS += "lk"

DEFAULTTUNE ?= "armv7a-neon"
include conf/machine/include/tune-cortexa8.inc

IMAGE_FSTYPES += "tar.bz2"

# For 2k page size nand flash
MKBOOTIMG_IMAGE_FLAGS_2K = ""

# For 4k page size nand flash
MKBOOTIMG_IMAGE_FLAGS_4K = "--pagesize 4096"

UBINIZE_ARGS_2k = '-m 2048 -p 128KiB -s 2048'
UBINIZE_ARGS_4k = '-m 4096 -p 256KiB -s 4096'

# Add squashfs file system
IMAGE_FSTYPES += "squashfs"
# EXTRA_IMAGECMD = "-b 65536 -info"

# UBI image generation happens in the "prepare_ubi" function
# (take a look at meta-swi/meta-swi-mdm9x15/recipes-core/images/mdm9x15-image.inc)
UBI_ROOTFS_PATH ?= "${IMGDEPLOYDIR}/${IMAGE_NAME}${IMAGE_NAME_SUFFIX}.squashfs"
UBI_ROOTFS_SIZE ?= "32MiB"

# For User1 MTD partition UBI image.
UBI_USER1_PATH ?= "${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.user1"
UBI_USER1_SIZE ?= "124MiB"

# Console used in mdm9x15 is managed by the modem through command AT!MAPUART
#SERIAL_CONSOLE = "ttyHSL1 115200 console"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
PREFERRED_VERSION_linux-yocto ?= "3.14%"

# GPLv2 readline
PREFERRED_VERSION_readline ?= "5.2"

# Known and supported kernel image types are:
# Image - uncompressed
# zImage - compressed
KERNEL_IMAGETYPE = "zImage"

# Tell the system to build device tree. Leave this empty if
# device tree is not needed. If empty, set KERNEL_ATTACHED_DEVICE_TREE
# to 0.
KERNEL_DEVICE_TREE_BLOB_NAME = "qcom-msm9615.dtb"

# Tell the system to attach device tree blob to kernel.
KERNEL_ATTACHED_DEVICE_TREE = "1"

# Various kernel boot options.

# ROOTFS is RAMDISK, attached to kernel
KERNEL_BOOT_OPTIONS_RAMDISK ?= "console=ttyHSL0,115200 console=ttyHSL1,115200 fudge_ro_rootfs=true rootfs_ro=true"

# ROOTFS is located on UBI volume, mtd3 partition. Set fudge_ro_rootfs to true, if unionfs should be mounted to help
# transition from RW to RO root file system.
KERNEL_BOOT_OPTIONS ?= "ubi.mtd=2 ubi.block=0,rootfs root=/dev/ubiblock0_0 ro rootfstype=squashfs console=ttyHSL0,115200 console=ttyHSL1,115200 fudge_ro_rootfs=true"

# Don't install the kernel image on the rootfs
RDEPENDS_kernel-base = ""

MACHINE_FEATURES = "apm usbgadget usbhost vfat alsa"

# Feature rtc creates a dependency on busybox hwclock but:
# a) rtc is not accessible
# b) hwclock use conflicts with time service.
# This line prevents rtc feature being backfilled.
MACHINE_FEATURES_BACKFILL_CONSIDERED = "rtc"

# Define SIERRA for userspace application
TARGET_CPPFLAGS = "-DSIERRA"

# Legato targets supported by this platform
LEGATO_ROOTFS_TARGETS ?= "wp85"

