#@TYPE: Machine
#@NAME: swi-mdm9x40-ar759x-rcy
#@DESCRIPTION: Machine configuration for swi-mdm9x40-ar759x-rcy systems

BASEMACHINE_QCOM = "mdm9640"
MACHINE_DTS_NAME = "msm9640"

# Ship all kernel modules by default
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules"

# lk bootloader is used by mdm9x40
EXTRA_IMAGEDEPENDS += "lk"

DEFAULTTUNE ?= "armv7a-neon"
include conf/machine/include/tune-cortexa8.inc

# If "on" Dm-verity will open and you should update both rootfs and kernel that are paired
# And this volume will be check by shell scripts:"build.sh"
# Set it as "DM_VERITY_ENCRYPT = off" to close Dm-verity.
DM_VERITY_ENCRYPT = "on"

IMAGE_ROOTFS_SIZE = "10240"

# Set file extended attributes for rootfs during compile, Default is off.
# If you want to open this feature you should define both "SMACK_ATTR_NAME" and "SMACK_ATTR_VALUE"
# Here is the sample:
# SMACK_ATTR_NAME = "SMACK64"
# SMACK_ATTR_VALUE = "_"

IMAGE_FSTYPES += "tar.bz2"

# For 2k page size nand flash
MKBOOTIMG_IMAGE_FLAGS_2K = ""

# For 4k page size nand flash
MKBOOTIMG_IMAGE_FLAGS_4K = "--pagesize 4096"

# Add ubifs file system
IMAGE_FSTYPES += "ubifs"
MKUBIFS_ARGS = "-m 4096 -e 253952 -c 2146 -F"

# Add squashfs file system
IMAGE_FSTYPES += "squashfs"
# EXTRA_IMAGECMD = "-b 65536 -info"

# UBI image generation happens in the "prepare_ubi" function
# (take a look at meta-swi/meta-swi-mdm9x40/recipes-core/images/mdm9x40-image.inc)
# rootfs size is not used for squashfs
UBI_ROOTFS_SIZE ?= "38MiB"

UBI_USRFS_PATH ?= "${IMGDEPLOYDIR}/${IMAGE_NAME}.usrfs.ubifs"

# Console used in mdm9x40 is managed by the modem through command AT!MAPUART
#SERIAL_CONSOLE = "ttyHSL1 115200 console"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-quic"

# GPLv2 readline
PREFERRED_VERSION_readline ?= "5.2"

# Known and supported kernel image types are:
# Image - uncompressed
# zImage - compressed
KERNEL_IMAGETYPE = "zImage"

# Various kernel boot options.

# ROOTFS is RAMDISK, attached to kernel
#KERNEL_BOOT_OPTIONS_RAMDISK ?= "console=ttyHSL0,115200 root=/dev/ram fudge_ro_rootfs=true rootfs_ro=true"
KERNEL_BOOT_OPTIONS_RAMDISK ?= "console=ttyHSL0,115200 console=ttyHSL1,115200 root=/dev/ram rootfs_ro=true user1_fs=ubifs verity=${DM_VERITY_ENCRYPT}"

# ROOTFS is located on UBI volume, mtd3 partition. Set fudge_ro_rootfs to true, if unionfs should be mounted to help
# transition from RW to RO root file system.
KERNEL_BOOT_OPTIONS ?= "ubi.mtd=2 ubi.block=0,rootfs root=/dev/ubiblock0_0 ro rootfstype=squashfs console=ttyHSL0,115200 console=ttyHSL1,115200"

# Don't install the kernel image on the rootfs
RDEPENDS_kernel-base = ""

MACHINE_FEATURES = "apm usbgadget usbhost vfat alsa"

# Feature rtc creates a dependency on busybox hwclock but:
# a) rtc is not accessible
# b) hwclock use conflicts with time service.
# This line prevents rtc feature being backfilled.
MACHINE_FEATURES_BACKFILL_CONSIDERED = "rtc"

# Define SIERRA for userspace application
TARGET_CPPFLAGS = "-DSIERRA"

# Legato targets supported by this platform
LEGATO_ROOTFS_TARGETS ?= "ar759x"
