#@TYPE: Machine
#@NAME: swi-mdm9x28
#@DESCRIPTION: Machine configuration for swi-mdm9x28 systems

BASEMACHINE_QCOM = "mdm9607"
MACHINE_DTS_NAME = "mdm9x28"

# Ship all kernel modules by default
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules"

# lk bootloader is used by mdm9x28
EXTRA_IMAGEDEPENDS += "lk"

# Uncomment the following line to enable the hard floating point abi. Note that
# this breaks some binary libraries and 3D (neither of which ship with
# meta-yocto). For maximum compatibility, leave this disabled.
#DEFAULTTUNE ?= "cortexa8hf-neon"
include conf/machine/include/tune-cortexa8.inc

IMAGE_FSTYPES += "tar.bz2"

# For 2k page size nand flash
YAFFS2_IMAGE_FLAGS_2K = ""
MKBOOTIMG_IMAGE_FLAGS_2K = ""

# For 4k page size nand flash
YAFFS2_IMAGE_FLAGS_4K = "-c 4096 -s 160"
MKBOOTIMG_IMAGE_FLAGS_4K = "--pagesize 4096"

# Add yaffs2 file system
IMAGE_FSTYPES += "2k.yaffs2 4k.yaffs2"
EXTRA_IMAGEDEPENDS += "yaffs2-utils-native"

# Add the yaffs2 imaging commands to the filesystem support for Bitbake as OE-Core doesn't support them directly...
IMAGE_CMD_2k.yaffs2 = "mkyaffs2image ${YAFFS2_IMAGE_FLAGS_2K} ${EXTRA_IMAGECMD} ${IMAGE_ROOTFS} ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.2k.yaffs2; chmod 644 ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.2k.yaffs2"
IMAGE_CMD_4k.yaffs2 = "mkyaffs2image ${YAFFS2_IMAGE_FLAGS_4K} ${EXTRA_IMAGECMD} ${IMAGE_ROOTFS} ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.4k.yaffs2; chmod 644 ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.4k.yaffs2"

# Add ubifs file system
IMAGE_FSTYPES += "ubifs"
MKUBIFS_ARGS = "-m 2048 -e 126976 -c 1208 -F"

# Add squashfs file system
IMAGE_FSTYPES += "squashfs"
# EXTRA_IMAGECMD = "-b 65536 -info"

# UBI image generation happens in the "prepare_ubi" function
# (take a look at meta-swi/meta-swi-mdm9x28/recipes-core/images/mdm9x28-image.inc)
# rootfs size is not used for squashfs
UBI_ROOTFS_SIZE ?= "38MiB"

UBI_USRFS_PATH ?= "${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.usrfs.ubifs"

# Console used in mdm9x28 is managed by the modem through command AT!MAPUART
#SERIAL_CONSOLE = "ttyHSL1 115200 console"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-quic"

# GPLv2 readline
PREFERRED_VERSION_readline ?= "5.2"

# Known and supported kernel image types are:
# Image - uncompressed
# zImage - compressed
KERNEL_IMAGETYPE = "zImage"

# Various kernel boot options.

# ROOTFS is RW YAFFS2, mtd2 partition.
# KERNEL_BOOT_OPTIONS ?= "root=/dev/mtdblock2 rw rootfstype=yaffs2 rootflags=tags-ecc-off console=ttyHSL0,115200 console=ttyHSL1,115200"

# ROOTFS is YAFFS2, mtd2 partition, ro. Set fudge_ro_rootfs to true, if unionfs should be mounted to help
# transition from RW to RO root file system.
# KERNEL_BOOT_OPTIONS ?= "root=/dev/mtdblock2 ro rootfstype=yaffs2 rootflags=tags-ecc-off console=ttyHSL0,115200 console=ttyHSL1,115200 fudge_ro_rootfs=true"

# ROOTFS is RAMDISK, attached to kernel
KERNEL_BOOT_OPTIONS_RAMDISK ?= "console=ttyHSL0,115200 root=/dev/ram fudge_ro_rootfs=true rootfs_ro=true"

# ROOTFS is located on UBI volume, mtd3 partition. Set fudge_ro_rootfs to true, if unionfs should be mounted to help
# transition from RW to RO root file system.
KERNEL_BOOT_OPTIONS ?= "ubi.mtd=13 ubi.block=0,rootfs root=/dev/ubiblock0_0 ro rootfstype=squashfs console=ttyHSL0,115200 console=ttyHSL1,115200 fudge_ro_rootfs=true"

# Don't install the kernel image on the rootfs
RDEPENDS_kernel-base = ""

MACHINE_FEATURES = "apm usbgadget usbhost vfat alsa"

# Feature rtc creates a dependency on busybox hwclock but:
# a) rtc is not accessible
# b) hwclock use conflicts with time service.
# This line prevents rtc feature being backfilled.
MACHINE_FEATURES_BACKFILL_CONSIDERED = "rtc"

# Define SIERRA for userspace application
TARGET_CPPFLAGS = "-DSIERRA"

# Legato targets supported by this platform
LEGATO_ROOTFS_TARGETS ?= "ar758x"
