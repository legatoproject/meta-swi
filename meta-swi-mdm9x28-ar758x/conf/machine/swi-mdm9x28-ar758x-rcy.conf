#@TYPE: Machine
#@NAME: swi-mdm9x28-ar758x
#@DESCRIPTION: Machine configuration for swi-mdm9x28 systems

BASEMACHINE_QCOM = "mdm9607"
MACHINE_DTS_NAME = "mdm9x28"

# Ship all kernel modules by default
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules"

# lk bootloader is used by mdm9x28
EXTRA_IMAGEDEPENDS += "lk"

# Uncomment the following line to enable the hard floating point abi. Note that
# this breaks some binary libraries and 3D (neither of which ship with
# meta-yocto). For maximum compatibility, leave this disabled.
#DEFAULTTUNE ?= "cortexa8hf-neon"
include conf/machine/include/tune-cortexa8.inc

IMAGE_FSTYPES += "tar.bz2"

# If "on" Dm-verity will open and you should update both rootfs and kernel at the same time
# Set it as "DM_VERITY_ENCRYPT = off" to close Dm-verity.
DM_VERITY_ENCRYPT ?= "on"

IMAGE_ROOTFS_SIZE = "10240"

# Set file extended attributes for rootfs during compile, Default is off.
# If you want to open this feature you should define both "SMACK_ATTR_NAME" and "SMACK_ATTR_VALUE"
# Here is the sample :
# SMACK_ATTR_NAME = "SMACK64"
# SMACK_ATTR_VALUE = "_"

# For 2k page size nand flash
MKBOOTIMG_IMAGE_FLAGS_2K = ""

# For 4k page size nand flash
MKBOOTIMG_IMAGE_FLAGS_4K = "--pagesize 4096"

# Add ubifs file system
IMAGE_FSTYPES += "2k.ubifs"
IMAGE_FSTYPES += "4k.ubifs"

MKUBIFS_ARGS_2k = "-m 2048 -e 126976 -c 1208 -F"
MKUBIFS_ARGS_4k = "-m 4096 -e 253952 -c 2146 -F"

IMAGE_CMD_2k.ubifs = "mkfs.ubifs -r ${IMAGE_ROOTFS} -o ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.2k.ubifs ${MKUBIFS_ARGS_2k}"
IMAGE_CMD_4k.ubifs = "mkfs.ubifs -r ${IMAGE_ROOTFS} -o ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.4k.ubifs ${MKUBIFS_ARGS_4k}"

# Add squashfs file system
IMAGE_FSTYPES += "squashfs"
# EXTRA_IMAGECMD = "-b 65536 -info"

# UBI image generation happens in the "prepare_ubi" function
# (take a look at meta-swi/meta-swi-mdm9x28/recipes-core/images/mdm9x28-image.inc)
# rootfs size is not used for squashfs
UBI_ROOTFS_SIZE ?= "38MiB"

UBI_USRFS_PATH ?= "${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.usrfs.ubifs"

# Console used in mdm9x28 is managed by the modem through command AT!MAPUART
#SERIAL_CONSOLE = "ttyHSL1 115200 console"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-quic"

# GPLv2 readline
PREFERRED_VERSION_readline ?= "5.2"

# Known and supported kernel image types are:
# Image - uncompressed
# zImage - compressed
KERNEL_IMAGETYPE = "zImage"

# Various kernel boot options.

# ROOTFS is RAMDISK, attached to kernel
KERNEL_BOOT_OPTIONS_RAMDISK ?= "console=ttyHSL0,115200 console=ttyHSL1,115200 root=/dev/ram rootfs_ro=true user1_fs=ubifs fudge_ro_rootfs=false verity=${DM_VERITY_ENCRYPT}"

# ROOTFS is located on UBI volume, mtd3 partition. Set fudge_ro_rootfs to true, if unionfs should be mounted to help
# transition from RW to RO root file system.
KERNEL_BOOT_OPTIONS ?= "ubi.mtd=13 ubi.block=0,rootfs root=/dev/ubiblock0_0 ro rootfstype=squashfs console=ttyHSL0,115200 console=ttyHSL1,115200"

# Don't install the kernel image on the rootfs
RDEPENDS_kernel-base = ""

MACHINE_FEATURES = "apm usbgadget usbhost vfat alsa"

# Feature rtc creates a dependency on busybox hwclock but:
# a) rtc is not accessible
# b) hwclock use conflicts with time service.
# This line prevents rtc feature being backfilled.
MACHINE_FEATURES_BACKFILL_CONSIDERED = "rtc"

# Define SIERRA for userspace application
TARGET_CPPFLAGS = "-DSIERRA"

# Legato targets supported by this platform
LEGATO_ROOTFS_TARGETS ?= "ar758x"
